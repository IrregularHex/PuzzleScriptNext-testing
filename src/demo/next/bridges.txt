title Bridges
author polyomino games
homepage polyomino.org

// join up the network using single and double lines
// port/rewrite from Tatham collection

// Demonstrates PS Next features: 
// * rules referring to OR lists of the same length are expanded pair-wise
// * 'once' means the rule is applied just once whether 'anything changed' or not
// * GOSUB and SUBROUTINE
// * text sprites
// * STATUS line
// * mouse clicks
// * sprites of different sizes scaled to fit

color_palette mastersystem
background_color blue
text_color yellow

nokeyboard
mouse_clicks
status_line

debug
verbose_logging

========
OBJECTS
========

edge *
blue green lightblue lightgreen darkgreen darkblue yellow red orange
012345678
123456780
234567801
345678012
456780123
567801234
678012345
780123456
801234567

cell
darkblue green
1110000000111
110.......011
10.........01
0...........0
0...........0
0...........0
0...........0
0...........0
0...........0
0...........0
10.........01
110.......011
1110000000111

singley
darkblue
....0....
....0....
....0....
....0....
....0....
....0....
....0....
....0....
....0....

singlex
darkblue
000000000

doubley
darkblue
...0.0...
...0.0...
...0.0...
...0.0...
...0.0...
...0.0...
...0.0...
...0.0...
...0.0...

doublex
darkblue
000000000
.........
000000000

// numbers in cells -- macro would help
N1 scale: 0.7; black; text: 1
N2 scale: 0.7; black; text: 2
N3 scale: 0.6; black; text: 3
N4 scale: 0.6; black; text: 4
N5 scale: 0.6; black; text: 5
N6 scale: 0.6; black; text: 6
N7 scale: 0.7; black; text: 7
N8 scale: 0.7; black; text: 8

c0; transparent; text: A
c1; transparent; text: B
c2; transparent; text: C
c3; transparent; text: D
c4; transparent; text: E 
c5; transparent; text: F 
c6; transparent; text: G
c7; transparent; text: H 
c8; transparent; text: I

Background .; green
player -; transparent
lit;    yellow
won;    transparent

=======
LEGEND
=======

number = n1 or n2 or n3 or n4 or n5 or n6 or n7 or n8
1 = n1 and cell
2 = n2 and cell
3 = n3 and cell
4 = n4 and cell
5 = n5 and cell
6 = n6 and cell
7 = n7 and cell
8 = n8 and cell
single = singley or singlex
double = doubley or doublex
linex = singlex or doublex
liney = singley or doubley
line = single or double

// map to add 1 or 2, in reverse order to propagation on rule expansions
count  = c8 or c7 or c6 or c5 or c4 or c3 or c2 or c1 or c0
count1 = c8 or c8 or c7 or c6 or c5 or c4 or c3 or c2 or c1
count2 = c8 or c8 or c8 or c7 or c6 or c5 or c4 or c3 or c2

=======
SOUNDS
=======

sfx1 3622903

================
COLLISIONLAYERS
================
Background
edge 
count lit
player
line
cell
number
won 

======
RULES
======

[ won ] -> cancel // it's already a win and we said so
[ player ] -> gosub player move
[ player ] -> gosub show lit
[ player ] -> gosub check win

////////////////////////////////////////////////////////////////////////////////
subroutine player move
// single line left right
horizontal [ cell    | lclick player no line ]           -> [ cell    | player action singlex ]
horizontal [ cell    | action singlex | player no line ] -> [ cell    | singlex | player action singlex ]
horizontal [ singlex | action singlex | player no line ] -> [ singlex | singlex | player action singlex ]

// promote single to double
horizontal [ cell    | lclick player singlex ]              -> [ cell    | player action doublex ]
horizontal [ cell    | action doublex | singlex ]           -> [ cell    | doublex | action doublex ]
horizontal [ doublex | action doublex | singlex ]           -> [ doublex | doublex | action doublex ]

// single line up down
vertical   [ cell    | rclick player no line ]           -> [ cell    | player action singley ]
vertical   [ cell    | action singley | player no line ] -> [ cell    | singley | player action singley ]
vertical   [ singley | action singley | player no line ] -> [ singley | singley | player action singley ]

// promote single to double
vertical   [ cell    | rclick player singley ]              -> [ cell    | player action doubley ]
vertical   [ cell    | action doubley | singley ]           -> [ cell    | doubley | action doubley ]
vertical   [ doubley | action doubley | singley ]           -> [ doubley | doubley | action doubley ]

// remove line
horizontal [ cell | lclick player doublex ] -> [ cell | player ]
vertical   [ cell | rclick player doubley ] -> [ cell | player ]

// remove loose ends
horizontal [ linex | player no line ] -> [ | player ]
vertical   [ liney | player no line ] -> [ | player ]

////////////////////////////////////////////////////////////////////////////////
subroutine show lit

// calculate a count for each cell, using the once prefix so the rule applies only once
            [ cell ] -> [ cell c0 ]
up    once [ cell count | singley ] -> [ cell count1 | singley ]
down  once [ cell count | singley ] -> [ cell count1 | singley ]
left  once [ cell count | singlex ] -> [ cell count1 | singlex ]
right once [ cell count | singlex ] -> [ cell count1 | singlex ]
up    once [ cell count | doubley ] -> [ cell count2 | doubley ]
down  once [ cell count | doubley ] -> [ cell count2 | doubley ]
left  once [ cell count | doublex ] -> [ cell count2 | doublex ]
right once [ cell count | doublex ] -> [ cell count2 | doublex ]

// light up every cell where the count matches
    [ lit ] -> [ ]
    [ 1 c1 ] -> [ 1 lit ]
    [ 2 c2 ] -> [ 2 lit ]
    [ 3 c3 ] -> [ 3 lit ]
    [ 4 c4 ] -> [ 4 lit ]
    [ 5 c5 ] -> [ 5 lit ]
    [ 6 c6 ] -> [ 6 lit ]
    [ 7 c7 ] -> [ 7 lit ]
    [ 8 c8 ] -> [ 8 lit ]

subroutine check win
[ player ] -> [ player won ]
[ cell no lit ][ player won ] -> [ cell ][ player ]
[ won ] -> sfx1
[ won ] -> status You win! Press R to play again, or Esc to select level.

==============
WINCONDITIONS
==============

//all cell on lit

=======
LEVELS
=======

message Left click to draw horizontal line, right click vertical

message The aim is to light up all the boxes

section Small 7x7 puzzle
***********
*.........*
*.3-----4.*
*.-------.*
*.3-5-2-4.*
*.-------.*
*.3-6---5.*
*.-------.*
*.4-3-2-4.*
*.........*
***********

section Medium 10x10 puzzle
**************
*............*
*.-2--3-2-4-.*
*.3--1-----2.*
*.--2-5---4-.*
*.5--4-2----.*
*.----------.*
*.-1-3-4--3-.*
*.4---6----5.*
*.----------.*
*.2---4---1-.*
*.---2-3---3.*
*............*
**************

section Large 15x15 puzzle
*******************
*.................*
*.2-5-----------3.*
*.---1-4-5---5-2-.*
*.2---1----2-----.*
*.-------------2-.*
*.2----2---------.*
*.----4--4-4-----.*
*.--4---4----5-4-.*
*.-------1-2-----.*
*.----------1----.*
*.--4---6--3-----.*
*.-------------3-.*
*.---------------.*
*.--2---4-2----2-.*
*.----3-----2----.*
*.2--3-------4--3.*
*.................*
*******************
