title ABRACADABRA 
author Andrea Gilbert
homepage clickmazes.com

background_color lightgrey
text_color darkblue
mouse_clicks
//level_select
status_line
noaction

sprite_size 12

========
OBJECTS
========

Background .; lightgrey

player
white transparent
000000000000
011111111110
011111111110
011111111110
011111111110
011111111110
011111111110
011111111110
011111111110
011111111110
011111111110
000000000000


1 scale: 0.8; red;    text: A
2 scale: 0.8; blue;   text: B
3 scale: 0.8; pink;   text: C
4 scale: 0.8; green;  text: D
5 scale: 0.8; orange; text: R

goal;   transparent
firstA; transparent
lastA;  transparent
step;   transparent

a; transparent
b; transparent
c; transparent
d; transparent
r; transparent

checkme; transparent

=======
LEGEND
=======

p = player and 1
q = goal   and 1
+ = firstA and a and step
* = lastA  and a
spell = a or b or c or d or r
tile = 1 or 2 or 3 or 4 or 5
//clickable = tile
keyA = firstA or lastA

=======
SOUNDS
=======
endlevel 86193703
sfx1 44745900
sfx2 9766906

================
COLLISIONLAYERS
================

Background
checkme
player step
goal keyA
spell tile 

======
RULES
======

[ > player ] -> cancel 

[ player | ... | lclick tile ] -> [ player | ... | tile checkme ]
[ lclick tile ] -> cancel sfx2

right [ step | A ] [ checkme 1 ] [ player ] ->  [ | step A ] [ 1 player ] [ ] sfx1
right [ step | B ] [ checkme 2 ] [ player ] ->  [ | step B ] [ 2 player ] [ ] sfx1
right [ step | C ] [ checkme 3 ] [ player ] ->  [ | step C ] [ 3 player ] [ ] sfx1
right [ step | D ] [ checkme 4 ] [ player ] ->  [ | step D ] [ 4 player ] [ ] sfx1
right [ step | R ] [ checkme 5 ] [ player ] ->  [ | step R ] [ 5 player ] [ ] sfx1
[ checkme ] -> cancel sfx2

//[ player q ][ step lastA ] -> status Congratulations!

=======
WINCONDITIONS
=======

all player on goal
all step on lastA

=======
LEVELS
=======

Message Spell the word ABRACADABRA \n\nStart on the top A \nFinish on the bottom A \nClick to move \n(any distance in a straight line)

...........
....p......
...1243....
...3412....
...4252....
...3551....
.....q.....
+bracadabr*
